DISTRO_NAME = "Embedded Connectivity Appliance"
DISTRO_VERSION = "0.9+snapshot-${DATE}"

VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = ""

include conf/distro/include/default-providers-eca.inc

DISTRO_FEATURES_append = " bluetooth systemd 3g wifi ipv6 nfc zeroconf"

LAYER_CONF_VERSION = "6"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = ""

DISTRO_FEATURES_BACKFILL_CONSIDERED="sysvinit"

# Enable some of the supported VPN technogies in ConnMan
PACKAGECONFIG_append_pn-connman = " openvpn l2tp pptp"

INHERIT += "extrausers"
EXTRA_USERS_PARAMS = "usermod -p '\$1\$1DO/lR8M\$f8JOOu4W8GBZzdNzCgSW40' root;"

include conf/distro/include/eca-edison.inc

# We have Broadcom bcm43340 wifi only in Edison
BBMASK .= "${@base_contains("MACHINE", "edison", "", "|meta-eca/meta-eca/recipes-kernel/bcm43340/", d)}"

# Use standard u-boot for Beaglebone
BBMASK .= "${@base_contains("MACHINE", "beaglebone", "|meta-eca/meta-eca-bsp/recipes-bsp/u-boot/", "", d)}"

# eca-image.bbappend has only stuff for edison or quark so mask those
# recipes away otherwise
BBMASK .= "${@base_contains("MACHINE", "edison quark", "", "|meta-eca/meta-eca-bsp/recipes-eca/images/", d)}"
